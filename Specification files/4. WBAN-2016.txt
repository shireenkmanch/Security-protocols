//----------------------------------------------------------------------------------------------------------------
//
//   Data Communication Protocol for WBAN
//   **************************************
//  “ Secure and Efficient Data Communication
//    Protocol for Wireless Body Area Networks “, 2016
// 
//----------------------------------------------------------------------------------------------------------------
//   Author: Shireen Kudukkil Manchingal
//   Date: 14 July 2017



// PROTOCOL SPECIFICATION
// ***********************
// S : Sensor
// D : Doctor
// DS : Data Sink
// Ksd : Random access token chosen by sensor
// Ksd1 : Shared secret between doctor and sensor
// KdPub : Public parameter generated by KGC to doctor
// KdPriv : Secret key distributed by KGC to doctor
// KsPub : Public parameter generated by KGC to sensor
// KsPriv : Secret key distributed by KGC to sensor
// TSs : Timestamp
// idD : ID of doctor
// idS : ID of sensor
// instruc : Instructions sent by doctor to sensor
// message : Data sent by sensor to doctor


// 1. 		Sensor : Ktdate = Ksd||TSs
//		Sensor : H = Hash(Ktdate)
//    Sensor -> Data Sink : idS, {{Ktdate}KdPub, H(Ktdate)}KsPriv
// 2. Data Sink -> Doctor : idS, {{Ktdate}KdPub}KsPriv
// 		Doctor : {{{Ktdate}KdPub}KsPriv}KsPub = {{Ktdate}KdPub}KdPriv = Ktdate
// 3. Doctor -> Sensor : idD, H(Ktdate)
//		Sensor : If H1 = H(Ktdate), Ktdate1 = Ksd1||TSs
//		Sensor : H2 = H(Ktdate1)
// 4. Sensor -> Data Sink : idS, {{Ktdate1}KdPub, H(Ktdate1)}KsPriv
// 5. Sensor -> Doctor : idS, {{Ktdate1}KdPub}KsPriv
// 		Doctor : {{{Ktdate1}KdPub}KsPriv}KsPub = {{Ktdate1}KdPub}KdPriv = Ktdate1
// 6. Doctor -> Sensor : idD, H(Ktdate1)
//		Sensor : If H2 = H(Ktdate1), continue
// 7. Doctor -> Sensor : {idD, idS, {instruc}Ksd1, H(Ksd1, instruc, idS)}KdPriv
//		Sensor : {{{instruc}Ksd1}KdPriv}KdPub = {{instruc}Ksd1}Ksd1 = instruc
//		Sensor : Computes H'(Ksd1, instruc, idS). If equal to H(Ksd1, instruc, idS),
//			 message integrity is proved.
// 8. Sensor -> Doctor : {idS, idD, {message}Ksd1}KsPriv




//-----------------------------------------------------------------------------------------------------------------



// 1.Initial AssumptioKsd
// **********************
// a.Express sensor's possessions/knowledge at time t0
A1: S possess at[0] Ksd;
A2: S possess at[0] Ksd1;
A3: S possess at[0] idS;
A4: S possess at[0] KsPub;
A5: S possess at[0] KsPriv;
A6: S possess at[0] message;
A7: S possess at[0] KdPub;
A8: S possess at[0] TSs;
A9: S know at[0] D possess at[0] KsPub;
A10: S know at[0] D possess at[0] idD;
A11: S know at[0] D possess at[0] KdPriv;
A12: S know at[0] not(Zero possess at[0] KsPriv);
A13: S know at[0] not(Zero possess at[0] Ksd);
A14: S know at[0] not(Zero possess at[0] Ksd1);
A15: S know at[0] not(Zero possess at[0] TSs);
A16: S know at[0] not(Zero possess at[0] idS);


// b.Express Doctor's possessions/knowledge at time t0
A17: D possess at[0] idD;
A18: D possess at[0] KdPub;
A19: D possess at[0] KdPriv;
A20: D possess at[0] instruc;
A21: D possess at[0] KsPub;
A22: D know at[0] S possess at[0] KdPub;
A23: D know at[0] S possess at[0] idS;
A24: D know at[0] S possess at[0] Ksd;
A25: D know at[0] S possess at[0] Ksd1;
A26: D know at[0] S possess at[0] KsPriv;
A27: D know at[0] not(Zero possess at[0] KdPriv);
A28: D know at[0] not(Zero possess at[0] idD);
A29: D know at[0] not(Zero possess at[0] Ksd1);


// b.Express Data Sink's possessions/knowledge at time t0
A30: DS possess at[0] KdPub;
A31: DS possess at[0] KsPub;
A32: DS know at[0] D possess at[0] KdPriv;
A33: DS know at[0] S possess at[0] KsPriv;
A34: DS know at[0] not(Zero possess at[0] TSs);
A35: DS know at[0] not(Zero possess at[0] Ksd);
A36: DS know at[0] not(Zero possess at[0] Ksd1);




// 2.Protocol Steps
// *****************
// a. Communication Establishment Phase
S1: DS receivefrom S at[1] {idS, {Ksd,TSs}KdPub, H(Ksd,TSs)}KsPriv;
S2: D receivefrom DS at[2] idS, {Ksd,TSs}KdPub;
S3: S receivefrom D at[3] {idD, H(Ksd,TSs)}KdPriv;
S4: DS receivefrom S at[4] {idS, {Ksd1,TSs}KdPub, H(Ksd1,TSs)}KsPriv;
S5: D receivefrom S at[5] {idS, {Ksd1,TSs}KdPub}KsPriv;
S6: S receivefrom D at[6] {idD, H(Ksd1,TSs)}KdPriv;

//b. Communication Phase
S7: S receivefrom D at[7] {idD, idS, {instruc}Ksd1, H(Ksd1, instruc, idS)}KdPriv;
S8: D receivefrom S at[8] {idS, idD, {message}Ksd1}KsPriv;




// 3.Protocol Goals
// *****************
G1: DS possess at[1] idS;
G2: DS possess at[1] {Ksd,TSs}KdPub;
G3: DS possess at[1] H(Ksd,TSs);
G4: DS know at[1] S send at[1] {idS, {Ksd,TSs}KdPub, H(Ksd,TSs)}KsPriv;
G5: DS know at[1] not(Zero possess at[0] {idS, {Ksd,TSs}KdPub, H(Ksd,TSs)}KsPriv);
G6: D possess at[2] idS;
G7: D possess at[2] Ksd;
G8: D possess at[2] TSs;
G9: D know at[2] S possess at[2] Ksd;
G10: D know at[2] DS send at[2] idS, {Ksd,TSs}KdPub;
G11: D know at[2] not(Zero possess at[0] idS, {Ksd,TSs}KdPub);
G12: S possess at[3] idD;
G13: S possess at[3] H(Ksd,TSs);
G14: S know at[3] D send at[3] {idD, H(Ksd,TSs)}KdPriv;
G15: S know at[3] not(Zero possess at[0] {idD, H(Ksd,TSs)}KdPriv);
G16: DS possess at[4] idS;
G17: DS possess at[4] {Ksd1,TSs}KdPub;
G18: DS possess at[4] H(Ksd1,TSs);
G19: DS know at[4] S send at[4] {idS, {Ksd1,TSs}KdPub, H(Ksd1,TSs)}KsPriv;
G20: DS know at[4] not(Zero possess at[0] {idS, {Ksd1,TSs}KdPub, H(Ksd1,TSs)}KsPriv);
G21: D possess at[5] idS;
G22: D possess at[5] Ksd1;
G23: D possess at[5] TSs;
G24: D know at[5] S possess at[5] Ksd1;
G25: D know at[5] S send at[5] {idS, {Ksd1,TSs}KdPub}KsPriv;
G26: D know at[5] not(Zero possess at[0] {idS, {Ksd1,TSs}KdPub}KsPriv);
G27: S possess at[6] idD;
G28: S possess at[6] H(Ksd1,TSs);
G29: S know at[6] D send at[6] {idD, H(Ksd1,TSs)}KdPriv; 
G30: S know at[6] not(Zero possess at[0] {idD, H(Ksd1,TSs)}KdPriv);
G31: S possess at[7] instruc;
G32: S possess at[7] H(Ksd1, instruc, idS);
G33: S know at[7] D send at[7] {idD, idS, {instruc}Ksd1, H(Ksd1, instruc, idS)}KdPriv;
G34: S know at[7] not(Zero possess at[0] {idD, idS, {instruc}Ksd1, H(Ksd1, instruc, idS)}KdPriv);
G35: D possess at[8] message;
G36: D know at[8] S send at[8] {idS, idD, {message}Ksd1}KsPriv;
G37: D know at[8] not(Zero possess at[0] {idS, idD, {message}Ksd1}KsPriv);


